include colorutil
include stringutil

dataclass GuessType
    let None = 0
    let Contains = 1
    let Correct = 2
end

class GuessChar
    let char
    let tp
    func new(char, tp)
        this.char = char
        this.tp = tp
    end
end

let readonly Words = StringUtil.Split(fread("words.txt"), "\n")

let readonly word = Words[int(rand() * (Words.length - 1))]

let guesses = 6 @ ( 5 @ nix )

let guess = 0

func printGameScreen()
    cls()
    ColorUtil.SetForegroundColor(ColorUtil.Colors.Green)
    emitln("EIWORDLE\n")
    ColorUtil.SetForegroundColor(ColorUtil.Colors.Gray)
    
    for i = 0 to 6 do
        ColorUtil.SetForegroundColor(ColorUtil.Colors.DarkGray)
        emit((i+1).asString + ". ")
        for j = 0 to 5 do
            if nix != guesses[i][j] then
                if (guesses[i][j]).tp ?= GuessType.Correct then
                    ColorUtil.SetForegroundColor(ColorUtil.Colors.Green)
                elif (guesses[i][j]).tp ?= GuessType.Contains then
                    ColorUtil.SetForegroundColor(ColorUtil.Colors.Yellow)
                else
                    ColorUtil.SetForegroundColor(ColorUtil.Colors.Red)
                end
                emit((guesses[i][j]).char)
            end
        end
        emit("\n")
    end
end

func processGuess(input)
    let counts = 26 @ 0
    let marked = 5 @ false

    for i = 0 to 5 do
        if input[i] ?= word[i] then
            guesses[guess][i] = GuessChar((input[i]), GuessType.Correct)
            marked[i] = true
        else
            guesses[guess][i] = GuessChar((input[i]), GuessType.None)
        end
    end

    for i = 0 to 5 do
        counts[ascii(word[i]) - 97] += 1
    end
    for i = 0 to 5 do
        if input[i] ?= word[i] then
            counts[ascii(word[i]) - 97] -= 1
        end
    end
    for i = 0 to 5 do
        if (not marked[i]) then
            if StringUtil.Contains(word, input[i]) then
                if counts[ascii(input[i]) - 97] > 0 then
                    guesses[guess][i] = GuessChar(guesses[guess][i].char, GuessType.Contains)
                    counts[ascii(input[i]) - 97] -= 1
                end
            end
        end
    end
end

while true do
    if guess >= 6 then
        brk
    end
    
    printGameScreen()
    let i = in()
    if i.length ?= 5 then
        processGuess(i)
        guess += 1
    end
    if i ?= word then
        printGameScreen()
        playing = false
    end
end 
emit("The word was... ")
ColorUtil.SetForegroundColor(ColorUtil.Colors.Green)
emitln(word)
ColorUtil.SetForegroundColor(ColorUtil.Colors.Gray)